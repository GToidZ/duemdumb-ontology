PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/lone_lee/ontologies/2023/10/DuemDumb#>

# This query answers the question 4,
# How much ABV is in Cocktail?

SELECT ?cocktail
	(sum(?avol) AS ?alcoholVol)
	(sum(?tvol) AS ?totalVol)
    ((sum(?avol)/sum(?tvol))*100 as ?abv)
    	WHERE {
		?cocktail rdf:type :Cocktail;
		:hasEntry ?e.

		?e :hasIngredient ?ingredient;
			:hasAmount ?amount;
			:useMeasurement ?measurement.

		OPTIONAL {
			?ingredient :hasABV ?abv_temp.
            # Some ingredients are not alcoholic so we can't always expect
            # values to be available.
		}
		OPTIONAL {
			?measurement rdf:type :LiquidMeasurement;
				 :milliliterMultiplier ?mm_temp.
            # Same goes to measurement, if it's not liquid measurement,
            # we cannot calculate them.
		}
		BIND(IF(bound(?mm_temp), ?amount * ?mm_temp, 0) AS ?tvol)
		BIND(IF(bound(?abv_temp), ?abv_temp / 100, 0) AS ?abv)
		
		BIND(IF(?abv > 0, ?abv * ?tvol, 0) AS ?avol)
}
GROUP BY ?cocktail