PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/lone_lee/ontologies/2023/10/DuemDumb#>

# This query answers the question 9,
# Can the ontology recommend cocktails for each occasions?

SELECT ?o ?c
WHERE {
    ?o rdf:type :Occasion;
        :standardDrinkThreshold ?trsh.
    
    # Retrieve cocktail and its total volume along with ABV.
    # (Yes, we reused the query from 'Listing Cocktail ABVs.')
    { SELECT ?c
	    (sum(?tvol) AS ?totalVol)
        ((sum(?avol)/sum(?tvol))*100 as ?abv)
    WHERE {
        ?c rdf:type :Cocktail;
            :hasEntry ?e.
        
        ?e :hasIngredient ?ingredient;
			:hasAmount ?amount;
			:useMeasurement ?measurement.

        OPTIONAL {
			?ingredient :hasABV ?abv_temp.
		}
		OPTIONAL {
			?measurement rdf:type :LiquidMeasurement;
				 :milliliterMultiplier ?mm_temp.
		}
		BIND(IF(bound(?mm_temp), ?amount * ?mm_temp, 0) AS ?tvol)
		BIND(IF(bound(?abv_temp), ?abv_temp / 100, 0) AS ?abv)
		
		BIND(IF(?abv > 0, ?abv * ?tvol, 0) AS ?avol)
    } GROUP BY ?c }
    
    BIND((?totalVol * (?abv / 100) * 0.79) / 10 AS ?standardDrink)  # Standard drink equivalence
    
    FILTER(?standardDrink <= ?trsh)  # Filter out drink that are less than or equal threshold.
}